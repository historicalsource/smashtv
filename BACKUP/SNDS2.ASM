	.FILE	'SOUNDS.ASM'
	.TITLE	'GSP SOUND PROCESSOR VER. 2.0'
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

**************************************************************************
*								         *
* 	     COPYRIGHT (C) 1990 MIDWAY MANUFACTURING COMPANY,		 *
* 	      MANUFACTURERS OF BALLY/MIDWAY AMUSEMENT GAMES.		 *
* 		         ALL RIGHTS RESERVED.				 *
*								         *
**************************************************************************

;
; GET THE SYSTEM STUFF

	.INCLUDE	"MPROC.EQU"		;MPROC EQUATES
	.INCLUDE	"\VIDEO\SYS\SYS.INC"	;Z UNIT SYSTEM EQUATES
	.INCLUDE	"\VIDEO\SYS\MACROS.HDR"	;MACROS DEFINITIONS
	.INCLUDE	"ROBO.EQU"		;SOUND HEADER

;
;
	.REF	SOUNDSUP
;
;SOUND DRIVER STUFF
;
	.DEF	SNDRES,ONESND,SNDLD,SNDSND,CKSNDPRI,QSNDRST
	.DEF 	MUSICOFF,YAMOFF,FADE_DOWN,ALLOFF,SNDPRC,FADE_UP
;
;SOUND HEADERS USED IN GAME
;
	.DEF	BTUNE1,TUNE1,TUNE2,DOOR1,SCASH,GIFT,YAMOFF,PZLSND
	.DEF	ICON,EXP3,SHOT,MISS,EXP1,TING,FSHOT,EXP3HI,TING2
	.DEF	MBANG,BLDS,UGH,BUBL,RIFL,HEXP,GDTH1
	.DEF	GDTH2,FREESND,CROWD1,CROWD2,SHKSND
	.DEF	GRNSND,LAZSND,BEEP2,MISS2,GLSEXP,BEEP1

	.TEXT
*
*SOUND PROCESSOR MAKES SOUNDS FOR N-CHANNELS (CURRENTLY 3):
*
*		CHANNEL #0	CVSD SOUNDS
*		CHANNEL #1	DAC 
*		CHANNEL #2	MUSIC
*		CHANNEL #3	FOREGROUND
*
*THE ROUTINE "SNDRES" MUST BE CALLED TO INITAILIZE SOUND BOARD
*ALSO SCRATCHPAD RAM MUST BE INITIALIZED TO ZERO
*THE ROUTINE "SNDPRC" MUST BE CALLED EVERY 16MSEC. IN THE MAIN PROGRAM LOOP
*(THE SAME PLACE WHERE "PRCDSP" IS NORMALLY CALLED)
*MAKING A SOUND:
*
*SOUND TABLE FORMAT
*
*SOUND DESCRIPTION:
*	.WORD PRIORITY, TIME, SOUND CODE, (PRIORITY), (TIME), (SOUND CODE), 0
*
*SOUND ENDS IN 0000.
*PRIORITY WORD = 1111IENNPPPPPPPP; I=1=NON-INTERRUPTABLE, E=1=NON-INT BY EQUAL
*NN=CHANNEL #(0-3);
*PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).
*SOUND WORD =4SSS SSS=SOUND CODE (ZERO TIME SOUND CODE)
*EXAMPLE 1:
*
*	MOVI	SND1,A0		;LOAD ABSOLUTE ADDRESS OF SND1 IN A0
*	CALLA	ONESND		;MAKE ONE SND1
*
*SND1:	.WORD	>F0FF,>0100,>8088,0	
*
*SND1 IS FOR CHANNEL=0, PRIORITY=FF (HIGHEST), TIME = 100H X 16MSEC.
*SOUND CODE = 88H
*SND1 WILL MAKE ONE SOUND 88H, AND NOT ALLOW IT TO BE INTERRUPTED 
*FOR 100H X 16MSEC., EXCEPT BY A SOUND OF EQUAL OR GREATER PRIORITY ON
*CHANNEL 0
*
*	MOVI	SND2,A0		;LOAD SND2 ADDRESS
*	MOVK	3,A1		;REPEAT IT 3 TIMES
*	CALLA	SNDLD		;GO DO IT

*SND2:	.WORD	>F311,>0020,>8044,>8044,0
*
*SND2 IS ON CHANNEL 3, PRIORITY 11H. IT WILL MAKE ONE SOUND 44H, WAIT
*20H X 16 MSEC., THEN MAKE ANOTHER SOUND 44H, WHICH CANNOT BE INTERRUPTED
*FOR 20H X 16 MSEC., EXCEPT BY A SOUND OF EQUAL OR GREATER PRIORITY ON
*CHANNEL 3
*
*SINCE A1 IS LOADED WITH A REPEAT COUNT OF 3, THIS PROCESS WILL BE REPEATED
*THREE TIMES. THE END RESULT WILL BE 6 SOUND 44H SEPERATED BY 20H X 16MSEC.
*
*NOTE: THE CHANNEL SPECIFIED BY "MUSICHAN" WILL CAUSE THE MUSIC SECTION OF
*	THE SOUND BOARD TO BE STROBED.
*
SNDPRI	.SET	0  	;PRIORITY (00=LOWEST, FF=HIGHEST)
SNDTIM	.SET	010h	;TIMER 000-7FFF
SNDTMR	.SET	020h	;TIMER COUNTDOWN
SNDST	.SET	030h	;ADDRESS OF SOUND TABLE START
SNDPTR	.SET	050h	;POINTER TO SOUND TABLE DATA
SNDREP	.SET	070h	;REPEAT COUNT OF SOUND
*NOTE: IF YOU CHANGE SNDSIZ, MAKE SURE ANY CODE THAT USES IT AS A DIVISOR 
*	PERFORMS THE CORRECT OP. CURRENTLY IT IS A POWER OF 2 SO SRL IS USED.
SNDSIZ	.SET	080h

NCHAN		EQU	4	;DECLARE NUMBER OF ALLOWED CHANNELS
MUSICHAN	EQU	2	;THIS CHANNEL IS FOR THE TUNES

*THESE STROBE EQUATES ARE FOR THE PINBALL SOUND CARD
RESETBIT	EQU	0FE00H	;THIS IS THE ^RESET BIT
MUSICBIT	EQU	0FD00H	;THIS IS THE YAMAHA SIDE STROBE
SOUNDBIT	EQU	0FD00H	;THIS IS THE SOUND SIDE STROBE

*THESE STROBE EQUATES ARE FOR THE NARC SOUND CARD
***RESETBIT	EQU	0FB00H	;THIS IS THE ^RESET BIT
***MUSICBIT	EQU	0FD00H	;THIS IS THE YAMAHA SIDE STROBE
***SOUNDBIT	EQU	0FE00H	;THIS IS THE SOUND SIDE STROBE

	.BSS	SNDSTR,NCHAN*SNDSIZ	;RESERVE STORAGE AREA
	.BSS	SNDEND,0		;END OF SOUND PROCESSOR RAM

**************************************************************************
*                                                                        *
* CLRSNDDB - CLEAR THE SOUND PROCESSOR DATA BASE			 *
*                                                                        *
**************************************************************************
CLRSNDDB
	MMTM	SP,A0,A1
	MOVI	SNDSTR,A1	
	CLR	A0
CLRSDBL
	MOVE	A0,*A1+,W
	CMPI	SNDEND,A1
	JRLO	CLRSDBL
	MMFM	SP,A0,A1
	RETS

*
*MAKE ONE SOUND
*A0=SOUND ADDRESS
ONESND:	MOVE	A1,-*SP,L
	MOVK	1,A1
	CALLR	SNDLD
	MOVE	*SP+,A1,L
	RETS
*
*SOUND LOADER
*A0=SOUND ADDRESS, A1=REPEAT COUNT
*SOUND DESCRIPTION= PRIORITY,TIME,SOUND CODE,(PRIORITY),(TIME),(SOUND CODE),0
*SOUND ENDS IN ZERO
*PRIORITY WORD = 1111IENNPPPPPPPP; I=1=NON-INTERRUPTABLE, E=1=NON-INT BY EQUAL
*NN=CHANNEL #(0-3);
*PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).
*SOUND WORD =4SSS SSS=SOUND CODE (ZERO TIME SOUND CODE)
SNDLD:
	MMTM	SP,A0,A1,A2,A3,A4,A5
	MOVE	@SOUNDSUP,A4,W
	JRNZ	SNDLDX		;SOMEBODY SAYS NO!

	MOVE	*A0,A5		;GET PRIORITY BYTE
	MOVE	A5,A2
	MOVI	SNDSIZ,A3
	SLL	22,A5
	SRL	30,A5		;EXTRACT CHANNEL BITS
	MPYU	A5,A3
	ADDI	SNDSTR,A3 	;CALC RAM CHANNEL ADDRESS
	MOVE	*A3(SNDPRI),A4 	;GET CURRENT PRIORITY
	BTST	11,A4		;CURRENT SOUND NON-INTERRUPTABLE ?
	JRNE	SNDLDX		;YES, CAN NEW SOUND
	MOVE	A2,A5		;EXTRACT PRIORITY
	SLL	24,A5
	SRL	24,A5
	SLL	24,A4
	SRL	24,A4
	CMP	A5,A4		;NEW ONE GREATER OR EQUAL?
	JRHI	SNDLDX		;OLD ONE IS GREATER, FORGET NEW SOUND
	JRLO	SNDLD1		;NEW ONE IS GREATER, DO IT
	MOVE	*A3(SNDPRI),A4	;INTERRUPTABLE BY EQUAL?
	BTST	10,A4		
	JRNE	SNDLDX		;NON-INTERRUPTABLE BY EQUAL, CAN NEW GUY
SNDLD1:
	MOVE	A0,A4
	MOVE	A3,A0			;CHANNEL RAM ADDRESS
	MOVE	A4,*A0(SNDPTR),L	;SETUP SOUND DATA POINTER (ROM)
	MOVE	A4,*A0(SNDST),L 	;SETUP SOUND START POINTER
	MOVE	A1,*A0(SNDREP)		;REPEAT COUNT       
	CALLR	SNDUPD			;START SOUND GOING
SNDLDX:
 	MMFM	SP,A0,A1,A2,A3,A4,A5
	RETS

*
*SOUND PROCESSOR CALLED EVERY 16 MSEC.
*
SNDPRC:	
	MOVI	SNDSTR,A0
	MOVK	NCHAN,A1
SNDLP0:
	MOVE	*A0(SNDTMR),A2 	;CHECK TIMER
	JREQ	SNDPLP		;EQUAL, INACTIVE CHANNEL
	DEC	A2
	MOVE	A2,*A0(SNDTMR)	;DEC TIME
	JRNE	SNDPLP		;NO TIMEOUT
	CALLR	SNDUPD		;UPDATE SOUND
SNDPLP:
	ADDI	SNDSIZ,A0
	DSJS	A1,SNDLP0
	RETS

*
*UPDATE SOUND
*A0=POINTER TO SOUND CHANNEL RAM
*
SNDUPD:
SNDUP0:
        MOVE	*A0(SNDPTR),A2,L	;GET POINTER TO ROM TABLE
SNDUP1:	
	MOVE	*A2+,A3		;GET NEXT ROM TABLE ENTRY
	JREQ	SNDUP5		;END OF SOUND
*CHECK FOR PRIORITY
	CMPI	->1000,A3
	JRLO	SNDUP2		;NOT PRIORITY CHANGE
	MOVE	A3,*A0(SNDPRI)	;UPDATE PRIORITY
	JRUC	SNDUP1		;GO GET NEXT ONE
*CHECK FOR SOUND CODE
SNDUP2:	
	CMPI	4000H,A3
	JRLO	SNDUP3
	CMPI	->8000,A3
	JRHS	SNDUP2A

	MOVE	A0,A5
	SUBI	SNDSTR,A5
	SRL	7,A5		;DIVIDE BY 128
	CALLR	SNDSND		;SEND SOUND CODE
	JRUC	SNDUP1		;GET THE NEXT ONE BOYS
SNDUP2A:	
	MOVE	A0,A5
	SUBI	SNDSTR,A5

	SRL	7,A5		;DIVIDE BY 128
**************************************************************************
*                                                                        *
* 	MOVI	SNDSIZ,A6	;IF SNDSIZ IS NOT A POWER OF 2,	       	 *
* 	DIVU	A6,A5		;USE DIVIDE TO CALCULATE SOUND CHANNEL # *
*                                                                        *
**************************************************************************
	CALLR	SNDSND		;SEND SOUND CODE

	MOVE	*A0(SNDTIM),*A0(SNDTMR)
	MOVE	A2,*A0(SNDPTR),L	;STORE POINTER
	RETS
*TIMER VALUE X 16MSEC.
SNDUP3:
	MOVE	A3,*A0(SNDTIM)	
	MOVE	A3,*A0(SNDTMR)
	JRUC	SNDUP1		;SET TIMER VALUE, AND GO GET NEXT
*CHECK FOR REPEATERS
SNDUP5:
	MOVE	*A0(SNDREP),A3	;CHECK REPEAT COUNT
	DEC	A3
	MOVE	A3,*A0(SNDREP)
	JREQ	SNDUP6		;ALL OVER
	MOVE	*A0(SNDST),*A0(SNDPTR)
	JRUC	SNDUP0		;START SOUND OVER
*SOUND IS OVER CLEAR THE CHANNEL
SNDUP6:
	CLR	A2
	MOVE	A2,*A0(SNDPRI)	;CLEAR SOUND PRIORITY	

SNDUPX:	RETS
*
*HARDWARE SOUND CALL
*A3=SOUND CODE B0-B7
*A5=SOUND CHANNEL #
*
SNDSND:
	MMTM	SP,A0,A3
	MOVI	8,A0		;SLOW DOWN FOR GRANNER & C0.
	DSJS	A0,$
	SLL	24,A3
	SRL	24,A3		;STRIP OFF BITS
	CMPI	MUSICHAN,A5
	JRNE	SNDSND1
	ADDI	MUSICBIT,A3	;PULL MUSIC SECTION INTERRUPT
	JRUC	SNDSND2
SNDSND1
	ADDI	SOUNDBIT,A3	;PULL SOUND SECTION INTERRUPT 
SNDSND2
	MOVE	A3,@SOUND	;HIT SOUND
	MOVI	8,A0
	DSJS	A0,$		;SLOW IT DOWN A BIT
	ORI	0FF00h,A3	;PUT OUT 1'S
	MOVE	A3,@SOUND
	MMFM	SP,A0,A3
	RETS	
*
*RESET SOUND BOARD, FULL RESET, CHECKSUM DINGS AND ALL
*
SNDRES:
	MMTM	SP,A0
	MOVI	RESETBIT,A0  	;HIT RESET BIT
	MOVE	A0,@SOUND
      	MOVI	100,A0		;WAIT FOR IT TO CATCH
	DSJS	A0,$	
	MOVI	0FFFFh,A0	;LET IT GO
	MOVE	A0,@SOUND
	MMFM	SP,A0
	RETS

**************************************************************************
*                                                                        *
* CKSNDPRI - CHECK SOUND CHANNEL FOR EXISTANCE OF A GIVEN SOUND PRIORITY *
* A0 = CHANNEL #							 *
* A1 = SOUND PRIORITY							 *
* RETURNS:								 *
* CARRY CLEAR = PRIORITY NOT ACTIVE					 *
* CARRY SET = PRIORITY ACTIVE						 *
*                                                                        *
**************************************************************************
CKSNDPRI
	MMTM	SP,A0,A2
	MOVI	SNDSTR,A2
	SLL	7,A0			;CHANNEL x 128
	ADD	A2,A0			;OFFSET TO CORRECT CHANNEL AREA
	MOVB	*A0,A0
	CMP	A0,A1
	JRNE	CKSPRI1			;BR = PRIORITY NOT ACTIVE
	SETC
	MMFM	SP,A0,A2
	RETS
CKSPRI1
	CLRC
	MMFM	SP,A0,A2
	RETS

**************************************************************************
*                                                                        *
* QSNDRST - QUIET SOUND RESET, NO DIAGNOSTIC DONGERS			 *
*                                                                        *
**************************************************************************
QSNDRST
	MMTM	SP,A3,A5
	CALLR	SNDRES		;RESET THE BOARD
	CLR	A3
	CLR	A5
	CALLR	SNDSND		;PEND ON DIG SIDE
	MOVK	MUSICHAN,A5
	CALLR	SNDSND		;PEND ON YAMAHA SIDE	
	MMFM	SP,A3,A5
	RETS

**************************************************************************
*                                                                        *
* FADE_UP - PROCESS TO FADE THE SOUND UP				 *
* A11 = FADE RATE							 *
*                                                                        *
**************************************************************************
FADE_UP:
	MOVI	FADE_UP_TAB,A8
	JRUC	FADER
**************************************************************************
*                                                                        *
* FADE_DOWN - PROCESS TO FADE THE SOUND UP				 *
* A11 = FADE RATE							 *
*                                                                        *
**************************************************************************
FADE_DOWN:
	MOVI	FADE_DOWN_TAB,A8
	JRUC	FADER

FADER:
	MOVI	FADEPID,A0
	CLR	A1
	NOT	A1
	CALLA	KILALL			;WASTE ANY OTHER FADERS
FADE_LP:
	MOVE	*A8+,A0,L
	JRZ	FADE_DONE
	CALLR	ONESND
	SLOOPR	A11,FADE_LP
FADE_DONE:
	DIE

FADE_UP_TAB:
	.LONG	VOLUME0
	.LONG	VOLUME1
	.LONG	VOLUME2
	.LONG	VOLUME3
	.LONG	VOLUME4
	.LONG	VOLUME5
	.LONG	VOLUME6
	.LONG	VOLUME7
	.LONG	VOLUME8
	.LONG	VOLUME9
	.LONG	VOLUMEA
	.LONG	VOLUMEB
	.LONG	VOLUMEC
	.LONG	VOLUMED
	.LONG	VOLUMEE
	.LONG	VOLUMEF
	.LONG	0

FADE_DOWN_TAB:
	.LONG	VOLUMEF
	.LONG	VOLUMEE
	.LONG	VOLUMED
	.LONG	VOLUMEC
	.LONG	VOLUMEB
	.LONG	VOLUMEA
	.LONG	VOLUME9
	.LONG	VOLUME8
	.LONG	VOLUME7
	.LONG	VOLUME6
	.LONG	VOLUME5
	.LONG	VOLUME4
	.LONG	VOLUME3
	.LONG	VOLUME2
	.LONG	VOLUME1
	.LONG	VOLUME0
	.LONG	0



**************************************************************************
*                                                                        *
* 		POPULAR SOUND CALLS 					 *
*                                                                        *
**************************************************************************
*
*HIGH PRIORITY OFF CODES, NOTHING SHOULD BEAT THESE
CVSDOFF	.WORD	>F0FF|>800,>01,>407D,0		;CVSD BACKGROUND LOOP OFF
;CVSDFOFF	.WORD	>F0FF|>800,>01,>4000,0		;CVSD FOREGROUND LOOP OFF
SOUNDOFF	.WORD	>F0FF|>800,>01,>4000,0		;TURN SOUND SIDE OFF
ALLOFF	 	.WORD	>F0FF|>800,>01,>4000,0		;ALL CVSD & DAC SOUNDS OFF
MUSICOFF 	.WORD	>F3FF|>800,>01,>407E,0		;MUSIC (ONLY) OFF
MUSITOFF 	.WORD	>F3FF|>800,>01,>4040,0		;MUSIC TRANSITION OFF	???
YAMOFF	 	.WORD	>F3FF|>800,>01,>407F,0		;YAMAHA EFFECT OFF
DIGOFF		.WORD	>F0FF|>800,>01,>4000,0		;TURN SOUND SIDE OFF
;YAMOFF	.WORD	>F3FF|>800,>01,>4000,0		;MUSIC HARD OFF

VOLUME0	.WORD	>F3FE,>1,>402F,>806F,0
VOLUME1	.WORD	>F3FE,>1,>402E,>806E,0
VOLUME2	.WORD	>F3FE,>1,>402D,>806D,0
VOLUME3	.WORD	>F3FE,>1,>402C,>806C,0
VOLUME4	.WORD	>F3FE,>1,>402B,>806B,0
VOLUME5	.WORD	>F3FE,>1,>402A,>806A,0
VOLUME6	.WORD	>F3FE,>1,>4029,>8069,0
VOLUME7	.WORD	>F3FE,>1,>4028,>8068,0
VOLUME8	.WORD	>F3FE,>1,>4027,>8067,0
VOLUME9	.WORD	>F3FE,>1,>4026,>8066,0
VOLUMEA	.WORD	>F3FE,>1,>4025,>8065,0
VOLUMEB	.WORD	>F3FE,>1,>4024,>8064,0
VOLUMEC	.WORD	>F3FE,>1,>4023,>8063,0
VOLUMED	.WORD	>F3FE,>1,>4022,>8062,0
VOLUMEE	.WORD	>F3FE,>1,>4021,>8061,0
VOLUMEF	.WORD	>F3FE,>1,>4020,>8060,0


;
* SMASH TV SOUND TABLE		

* CHANNEL #0	CVSD SOUNDS
* CHANNEL #1	DAC A						
* CHANNEL #2	MUSIC						
* CHANNEL #3 FOREGROUND
*------------------------------------------------------------------------*
*SOUND DESCRIPTION= PRIORITY,TIME,SOUND CODE,(PRIORITY),(TIME),(SOUND CODE),0
*SOUND ENDS IN 0000.
*PRIORITY WORD = 1111IENNPPPPPPPP; I=1=NON-INTERRUPTABLE, E=1=NON-INT BY EQUAL
*NN=CHANNEL #(0-3);
*PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).
*SOUND WORD =4SSS SSS=SOUND CODE (ZERO TIME SOUND CODE)
*------------------------------------------------------------------------*
SNDTAB:

TUNE1 		.WORD 	>F280,>012C,>8003,0		;PODIUM TO FIRST ARENA TUNE
DOOR1		.WORD 	>F376,>0005,>8083,0		;DOOR SLIDING OPEN
DOOR2		.WORD 	>F376,>0005,>8084,0		;DOOR SLIDING SHUT
FSHOT		.WORD 	>F7A0,>002E,>8085,0  		;POD SIZZLE
TUNE2		.WORD 	>F27E,>0078,>8001,0		;FIRST BKRND TUNE
ICON		.WORD 	>F4AF,>001E,>8091,0		;WEAPON ICON PICKUP (cvsd ch)
GDTH2		.WORD 	>F3AF,>001E,>8095,0		;GRUNT DEATH
SHOT		.WORD 	>F0E7,>000A,>80C4,0		;REGULAR SHOT
RIFL		.WORD 	>F4E8,>0012,>80C5,0		;RIFLE SPRAY WEAPON
MBANG 		.WORD 	>F5E9,>002E,>80A0,0		;ARM BANG
HEXP  		.WORD 	>F3A0,>0010,>808A,0		;OTHER HULK DEATH
HEXP2 		.WORD 	>F3A0,>001E,>809C,0		;OTHER HULK DEATH
HEXP3 		.WORD 	>F3A0,>001E,>809E,0		;OTHER HULK DEATH
BUBL		.WORD 	>FBFB,>001E,>80A3,0		;BLOOD BUBBLE
FREESND		.WORD 	>F8F7,>004B,>80A6,0		;FREE MAN SND (cvsd ch)
LAZSND		.WORD 	>F3F7,>0025,>80AE,0		;LAZER BOLTS FROM EYES
BEEP2		.WORD 	>F3F7,>0010,>80A4,0		;BEEP TONE
BEEP1		.WORD 	>F3F7,>0008,>80B0,0		;BEEP TONE
GRNSND		.WORD 	>F7F7,>0010,>80AA,0		;GRENADE LAUNCH SND
MISS		.WORD 	>F4E8,>000B,>80C3,0		;MISSLES
MISS2		.WORD 	>F0E7,>000B,>80C3,0		;MISSLES FROM BAD GUYS
BLDS		.WORD 	>FBDF,>001E,>808B,0		;BLADES
TING		.WORD 	>F38B,>001E,>809A,0		;BULLET HITTING WALL
TING2		.WORD 	>F3F3,>000F,>809A,0		;BULLET HITTING WALL
GIFT		.WORD 	>F3A0,>0032,>8081,0		;PRESENT PICKUP
SCASH		.WORD 	>F4A5,>001C,>8080,0		;CASH REGISTER SOUND 
GDTH1		.WORD 	>F3E4,>002D,>8082,0		;GRUNT DEATH
BTUNE1		.WORD 	>F27E,>0120,>8004,0		;MUTOID BOSS TUNE
EXP1		.WORD 	>F0E8,>000F,>80C2,0		;EXPLOSION SMALL
EXP3		.WORD 	>F0E8,>000D,>80C0,0		;EXPLOSION LARGE
EXP3HI		.WORD 	>F4FE,>0012,>80C0,0		;EXPLOSION LARGE FOR SMRT BMB
UGH		.WORD	>F8FF,>0014,>80C9,0		;PLAYER DIE SCREAM
GLSEXP		.WORD	>F0E8,>0012,>80CD,0		;GLASS SHATTER
SHKSND		.WORD	>FCFF,>0078,>80CF,0		;PLAYER ELECTRIC SHOCK 
CROWD1	 	.WORD	>F8F8,>004E,>80CA,0		;CROWD NOISE CHEERS
CROWD2 		.WORD 	>F8F8,>004E,>80CB,0		;CROWD NOISE OOHS N AAHS

;
;SPEECH
; Note: this should work: check to see that speech is *not* delayed .8 secs
; Speech will kill current foreground effects & any other CVSD Sound
SVCR		.WORD 	>FCFF,>0046,>407F,>8038,0		;VCR
STSTR		.WORD 	>FCFF,>003F,>407F,>8036,0		;TOASTER
SLUGG		.WORD 	>FCFF,>003C,>407F,>8033,0		;LUGGAGE
SHGM		.WORD 	>FCFF,>004F,>407F,>8031,0		;HOME GAME
STELE		.WORD 	>FCFF,>0047,>407F,>8035,0		;TV
SMWR		.WORD 	>FCFF,>0044,>407F,>8032,0		;RIDING MOWER
SCAR 		.WORD 	>FCFF,>0033,>407F,>8030,0		;CAR
SMEAT		.WORD 	>FCFF,>002A,>407F,>8034,0		;MEAT
STRIP		.WORD 	>FCFF,>0038,>407F,>8037,0		;TRIP

;
;SPEECH TABLES
;
PZLSND	.LONG	PZ1SND,PZ2SND,PZ3SND
PZ1SND	.LONG	STSTR,SCAR,SVCR
PZ2SND	.LONG	STELE,SLUGG,STRIP
PZ3SND	.LONG	SHGM,SMWR,SMEAT


	.END
